plugins {
    id 'maven'
    id 'java'
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow"
}

ext.moduleName = "${group}.imgui_gl"

dependencies {
    implementation "$kot-stdlib"
    implementation project(':imgui-core'), project(':imgui-glfw')

    implementation "${kx}.glm:glm:$glm_version"
    implementation "$kx:gln:$gln_version"
    implementation "$kx:kool:$kool_version"
    // implementation "$kx:kotlin-unsigned:$unsigned_version"
    ["core", "gl"].each {
        implementation "${kx}.uno-sdk:uno-$it:$uno_version"
    }

    ["", "-jemalloc", "-glfw", "-opengl", "-remotery", "-stb"].each {
        String base = "org.lwjgl:lwjgl$it:$lwjgl_version"
        implementation base
        String natives = "$base:natives-$lwjgl_natives"
        testRuntime natives
        shadow natives
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task lightJar(type: Jar) {
    archiveClassifier = 'light'
    from sourceSets.main.output
    exclude 'extraFonts'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives lightJar
    archives sourcesJar
//    archives javadocJar
}

shadowJar {
    exclude 'module-info.class'
    inputs.property("moduleName", moduleName)
//    manifest.attributes('Automatic-Module-Name': moduleName)
}

jar {
    inputs.property("moduleName", moduleName)
//    manifest.attributes('Automatic-Module-Name': moduleName)
}

shadowJar.archiveClassifier = 'all'

compileKotlin{
    kotlinOptions.jvmTarget = "11"
    destinationDir = compileJava.destinationDir
}
compileTestKotlin{
    kotlinOptions.jvmTarget = "11"
    destinationDir = compileTestJava.destinationDir
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}